buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

repositories {
    maven {
        url "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        url 'http://maven.epoxide.xyz'
    }
    maven {
        url 'http://dogforce-games.com/maven'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

version = "1.0"
group= "gigaherz.everpipe" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "Everpipe"

minecraft {
    version = "1.12.1-14.22.0.2452"
    runDir = "run"

    mappings = "snapshot_20170801"

    replace "@VERSION@", project.version
    replaceIn "Everpipe.java"
}

configurations {
    // configuration that holds jars to embed inside the jar
    embed
}

dependencies {
    deobfCompile "gigaherz.commons:gigaherz.commons-1.12.1:0.6.4"
    embed "gigaherz.commons:gigaherz.commons-1.12.1:0.6.4"

    deobfCompile "gigaherz.graph:GraphLib:1.3.1"
    embed "gigaherz.graph:GraphLib:1.3.0"

    deobfCompile "net.darkhax.tesla:Tesla-1.12:1.0.61"

    deobfCompile "mezz.jei:jei_1.12:4.7.4.80:api"
    runtime "mezz.jei:jei_1.12:4.7.4.80"

    compile fileTree(dir: 'libs', include: '*.jar')
}

jar {
    into('/') {
        from configurations.embed
    }

    manifest {
        attributes("ContainedDeps": configurations.embed.collect { it.getName() }.join(' '))
    }
}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.encoding = 'UTF-8'
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
