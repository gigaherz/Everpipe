buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '1.2.3'
}

repositories {
    maven {
        url "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        url 'http://maven.epoxide.xyz'
    }
    maven {
        url 'http://dogforce-games.com/maven'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

version = "1.0"
group= "gigaherz.everpipe" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "Everpipe"

minecraft {
    version = "1.11.2-13.20.0.2252"
    runDir = "run"

    mappings = "snapshot_20170226"

    replace "@VERSION@", project.version
    replaceIn "Everpipe.java"
}

jar {
    classifier = 'slim'
}

shadowJar {
    classifier = ''
    dependencies {
        include(dependency(':GraphLib:'))
        include(dependency(':Commons:'))
    }
    relocate 'gigaherz.common', 'gigaherz.everpipe.shadow.common'
}

reobf {
    shadowJar { mappingType = 'SEARGE' }
}

tasks.build.dependsOn reobfShadowJar

artifacts {
    archives shadowJar
    archives sourceJar
}

dependencies {
    deobfCompile "gigaherz.commons:Commons-1.11.2:0.5.2"
    deobfCompile "gigaherz.graph:GraphLib:1.1.1"

    deobfCompile "net.darkhax.tesla:Tesla:1.11-1.3.0.51"
    deobfCompile "mezz.jei:jei_1.11.2:4.2.10.248:api"
    runtime "mezz.jei:jei_1.11.2:4.2.10.248"

    compile fileTree(dir: 'libs', include: '*.jar')
}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.encoding = 'UTF-8'
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
